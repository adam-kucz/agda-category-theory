{-# OPTIONS --exact-split --prop #-}
module Category.FunCategory where

open import Category.Definition
open import Functor hiding (Id) renaming (_o_ to _âŠš_)
open import NaturalTransformation renaming (Identity to Id; Composition to _o_)
open import Category.ArrowCategory

open import Universes hiding (X; Y; Z)
open import Logic hiding (_,_)
open import Proof

open import Axiom.FunctionExtensionality

FunCategory :
  (â„‚ : Category ğ’° ğ’±)
  (ğ”» : Category ğ’² ğ’¯)
  â†’ --------------------
  Category (ğ’° âŠ” ğ’± âŠ” ğ’² âŠ” ğ’¯) (ğ’° âŠ” ğ’± âŠ” ğ’² âŠ” ğ’¯)
FunCategory â„‚ ğ”» = record
  { obj = Functor â„‚ ğ”»
  ; _~>_ = Î» F G â†’ F âŸ¹ G
  ; id = Id
  ; _âˆ˜_ = _o_
  ; left-unit = Î» {X} {Y} Î¸ â†’
    âŸ¹== (Id Y o Î¸) Î¸ $ subrel $ fun-ext Î» Z â†’ subrel $ left-unit (Î¸ at Z)
  ; right-unit = Î» {F} {G} Î¸ â†’
    âŸ¹== (Î¸ o Id F) Î¸ $ subrel $ fun-ext Î» Z â†’ subrel $ right-unit (Î¸ at Z)
  ; assoc = Î» Ïˆ Ï• Î¸ â†’ 
    âŸ¹== (Ïˆ o (Ï• o Î¸)) ((Ïˆ o Ï•) o Î¸) $
      subrel $ fun-ext Î» Z â†’ subrel $ assoc (Ïˆ at Z) (Ï• at Z) (Î¸ at Z)
  }
  where instance _ = â„‚; _ = ğ”»

open import Category.Product
open import Type.Sum hiding (_Ã—_)

App :
  (â„‚ : Category ğ’° ğ’±)
  (ğ”» : Category ğ’² ğ’¯)
  â†’ ------------------------------
  Functor (FunCategory â„‚ ğ”» Ã— â„‚) ğ”»
App â„‚ ğ”» = record
  { Fâ‚€ = Î» { (F , X) â†’ Fâ‚€ â¦ƒ F â¦„ X }
  ; Fâ‚ = Î» { {F , _}{_ , Y} (Î¸ , f) â†’ Î¸ at Y âˆ˜ Fâ‚ â¦ƒ F â¦„ f }
  ; id-preserv = Î» { (F , X) â†’ let instance _ = F in
      proof id (Fâ‚€ _) âˆ˜ Fâ‚ (id X)
        === id (Fâ‚€ _) âˆ˜ id (Fâ‚€ X)
          :by: ap (id (Fâ‚€ _) âˆ˜_) $ id-preserv X
        === id (Fâ‚€ X) :by: left-unit (id (Fâ‚€ X))
      qed }
  ; âˆ˜-preserv = Î» { {F , X} {G , Y} {H , Z} (Î¸ , g) (Ïˆ , f) â†’
      let instance _ = F in
      proof (Î¸ âˆ˜ Ïˆ) at Z âˆ˜ Fâ‚ (g âˆ˜ f)
        === Î¸ at Z âˆ˜ Ïˆ at Z âˆ˜ (Fâ‚ g âˆ˜ Fâ‚ f)
          :by: ap ((Î¸ âˆ˜ Ïˆ) at Z âˆ˜_) $ âˆ˜-preserv g f
        === Î¸ at Z âˆ˜ Ïˆ at Z âˆ˜ Fâ‚ g âˆ˜ Fâ‚ f
          :by: assoc _ _ _
        === Î¸ at Z âˆ˜ (Ïˆ at Z âˆ˜ Fâ‚ g) âˆ˜ Fâ‚ f
          :by: ap (_âˆ˜ Fâ‚ f) $ sym $ assoc (Î¸ at Z) (Ïˆ at Z) (Fâ‚ g)
        === Î¸ at Z âˆ˜ (Fâ‚ â¦ƒ G â¦„ g âˆ˜ Ïˆ at Y) âˆ˜ Fâ‚ f
          :by: ap (Î» â€” â†’ Î¸ at Z âˆ˜ â€” âˆ˜ Fâ‚ f) $ naturality â¦ƒ Ïˆ â¦„ g
        === Î¸ at Z âˆ˜ Fâ‚ â¦ƒ G â¦„ g âˆ˜ Ïˆ at Y âˆ˜ Fâ‚ f
          :by: ap (_âˆ˜ Fâ‚ f) $ assoc (Î¸ at Z) (Fâ‚ â¦ƒ G â¦„ g) (Ïˆ at Y)
        === (Î¸ at Z âˆ˜ Fâ‚ â¦ƒ G â¦„ g) âˆ˜ (Ïˆ at Y âˆ˜ Fâ‚ f)
          :by: sym $ assoc _ _ _
      qed}
  }
  where instance
          _ = â„‚
          _ = ğ”»
          _ = FunCategory â„‚ ğ”»

Cur : (F : Functor (â„‚ Ã— ğ”») ğ”¼) â†’ Functor â„‚ (FunCategory ğ”» ğ”¼)
Cur {â„‚ = â„‚}{ğ”» = ğ”»}{ğ”¼ = ğ”¼} F =
  [Fâ‚€= with-left
  ,Fâ‚= nat-trans
  ,id-pres= (Î» X â†’ âŸ¹== (nat-trans (id X)) (Id (with-left X)) $
               subrel $ fun-ext Î» Y â†’ subrel $ id-preserv (X , Y))
  ,âˆ˜-pres= (Î» g f â†’ âŸ¹== (nat-trans (g âˆ˜ f)) (nat-trans g o nat-trans f) $
              subrel $ fun-ext Î» Y â†’ subrel (
                proof Fâ‚ (g âˆ˜ f , id Y)
                  === Fâ‚ (g âˆ˜ f , id Y âˆ˜ id Y)
                    :by: ap (Î» â€” â†’ Fâ‚ (g âˆ˜ f , â€”)) $
                         sym $ left-unit (id â¦ƒ ğ”» â¦„ Y)
                  === Fâ‚ (g , id Y) âˆ˜ Fâ‚ (f , id Y)
                    :by: âˆ˜-preserv (g , id Y) (f , id Y) [: _==_ ]
                qed)) ]
  where instance _ = â„‚; _ = ğ”»; _ = ğ”¼; _ = F
        with-left : (X : obj â¦ƒ â„‚ â¦„) â†’ Functor ğ”» ğ”¼
        with-left X =
          [Fâ‚€= (Î» Xâ‚ â†’ Fâ‚€ (X , Xâ‚))
          ,Fâ‚= (Î» f â†’ Fâ‚ (id X , f))
          ,id-pres= (Î» Xâ‚ â†’ id-preserv (X , Xâ‚))
          ,âˆ˜-pres= (Î» g f â†’
            proof Fâ‚ (id X , g âˆ˜ f)
              === Fâ‚ (id X âˆ˜ id X , g âˆ˜ f)
                :by: ap (Î» â€” â†’ Fâ‚ (â€” , g âˆ˜ f)) $ sym $ left-unit (id X)
              === Fâ‚ (id X , g) âˆ˜ Fâ‚ (id X , f)
                :by: âˆ˜-preserv (id X , g) (id X , f)
              qed)]
        nat-trans : âˆ€{X Y}(f : X ~> Y) â†’ with-left X âŸ¹ with-left Y
        nat-trans {X}{Y} f = record
          { _at_ = Î» X' â†’ Fâ‚ (f , id X')
          ; naturality = Î» {X'}{Y'} f' â†’
            proof Fâ‚ (f , id Y') âˆ˜ Fâ‚ â¦ƒ with-left X â¦„ f'
              === Fâ‚ (f , id Y') âˆ˜ Fâ‚ (id X , f')
                :by: refl _
              === Fâ‚ (f âˆ˜ id X , id Y' âˆ˜ f')
                :by: sym $ âˆ˜-preserv (f , id Y') (id X , f')
              === Fâ‚ (f , f')
                :by: ap2 (Î» x y â†’ Fâ‚ (x , y)) (right-unit f) (left-unit f')
              === Fâ‚ (id Y âˆ˜ f , f' âˆ˜ id X')
                :by: sym $ ap2 (Î» x y â†’ Fâ‚ (x , y)) (left-unit f) (right-unit f')
              === Fâ‚ (id Y , f') âˆ˜ Fâ‚ (f , id X')
                :by: âˆ˜-preserv (id Y , f') (f , id X')
              === Fâ‚ â¦ƒ with-left Y â¦„ f' âˆ˜ Fâ‚ (f , id X')
                :by: refl _
            qed }
